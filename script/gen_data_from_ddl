#!/usr/bin/env perl

use strict;
use warnings;

use Data::Dumper;
use Data::FromDDL;
use Data::FromDDL::Util qw(
    normalize_parser_str
);
use Pod::Usage;
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);

my $n;
my $out = '';
my $parser = 'MySQL';
my $help = 0;
my $include = '';
my $exclude = '';
GetOptions(
    "num|n=s" => \$n,
    "help|h" => \$help,
    "out|o=s" => \$out,
    "parser|p=s" => \$parser,
    "include|i=s" => \$include,
    "exclude|e=s" => \$exclude,
) or pod2usage(2);

pod2usage(1)
    if $help;
pod2usage("Can't specify both of --include and --exclude options")
    if $include && $exclude;

sub parse_num_option {
    # $num_option_str is like 'users:20,bugs:30,100'
    my $num_option_str = shift;
    my @nums = split ',', $num_option_str;

    my $parsed = {
        all => undef,
        tables => {},
    };
    for (@nums) {
        my ($table, $n) = split ':', $_;
        if ($n) {
            $parsed->{tables}{$table} = $n;
        } else {
            # $table contains number
            $parsed->{all} = $table;
        }
    }
    return $parsed;
}

my $ddl = do {
    local $/;
    if (@ARGV) {
        # read from multiple files
        my $ddl_str = '';
        for my $ddl_file (@ARGV) {
            open my $fh, '<', $ddl_file
                or die("Can't open $ddl_file to read\n");
            $ddl_str .= <$fh>;
        }
        $ddl_str;
    } else {
        <STDIN>;
    }
};

my $out_fh;
if ($out) {
    open $out_fh, '>', $out
        or die("Can't open $out to write\n");
} else {
    $out_fh = *STDOUT;
}

$parser = normalize_parser_str($parser);

my @include = split ',', $include;
my @exclude = split ',', $exclude;

my $generator = Data::FromDDL->new(
    ddl => $ddl,
    parser => $parser,
    include => \@include,
    exclude => \@exclude,
);
$generator->generate($n, $out_fh);

__END__

=encoding utf-8

=head1 NAME

gen_data_from_ddl - Using Data::Generator::FromDDL

=head1 SYNOPSYS

gen_data_from_ddl [options]

 Options:
   -h|--help

=head1 OPTIONS

=over 8

=item B<-h | --help>

Print help message and exits.

=item B<-n | --num>

Generate B<num> records.

You can specify number of records of each table. 

Example:
--num=20 => 20 records are generated in all tables.
--num=users:10,blogs:50,100 => 10 records for users table, 50 records for blogs table, and 100 records for other tables.


=cut
